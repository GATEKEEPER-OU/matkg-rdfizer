prefix rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix helifit: <https://opensource.samsung.com/projects/helifit/>

SELECT DISTINCT ?userID ?sleepActivity ?sleepCycle ?sleepStage
        ?stageTypeNote ?start ?end ?sleepDuration
WHERE {
  ?sleepActivity rdf:type helifit:HLF43SleepingActivity .
  ?sleepActivity helifit:P14carriedOutBy ?ind .
  ?ind rdf:type helifit:E21Person .
  ?ind helifit:P1isIdentifiedBy ?userID .

  ?sleepActivity helifit:EP2initiates ?sleepCycle .
  ?sleepCycle rdf:type helifit:HLF20SleepCycle .

  ?sleepStage rdf:type helifit:HLF10SleepStage .
  ?sleepCycle helifit:EP3maintains ?sleepStage .

  ?stateType rdf:type helifit:E42Identifier .
  ?sleepStage helifit:P1isIdentifiedBy ?stateType .
  ?stateType helifit:P3hasNote ?stageTypeNote .

  ?stateTimeSpan rdf:type helifit:E52TimeSpan .
  ?sleepStage helifit:P4hasTimeSpan ?stateTimeSpan .
  ?stateTimeSpan helifit:EP7starts ?start .
  ?stateTimeSpan helifit:EP8ends ?end .
  BIND(MINUTES(?end-?start) AS ?sleepDuration)
 }
